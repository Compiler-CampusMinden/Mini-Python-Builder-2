plugins {
    id 'application'
}

group = 'mini-python-builder'

repositories {
    mavenCentral()
}

application {
    mainClass = 'minipython.builder.cbuilder.run.ManualTest'
}

// put the script-name -> mainClass relations
// into a directory with scriptName = file name & mainClass = file content
// in order to allow the makefile get info about the
// list of scripts...
def extraMainClasses = fileTree("${project.projectDir}/main-classes").collectEntries { extraMainClassDef ->
    [extraMainClassDef.name, extraMainClassDef.getText('utf-8').trim()]
}
// the examples have more than one main class,
// allow running each of them in the generated distribution.
// could also do something similar to define additional run tasks;
// but: experience shows that gradle likes to 'swallow'
// the stdout of run executions - as these classes
// tend to be used for testing let's just don't
extraMainClasses.each { scriptName, driverClass ->
    Task t = task("startScripts_" + scriptName,
                group: 'CLI Script Generation',
                type: CreateStartScripts) {
        applicationName = scriptName
        mainClass = driverClass

        classpath = startScripts.classpath
        defaultJvmOpts = startScripts.defaultJvmOpts
        executableDir = startScripts.executableDir
        optsEnvironmentVar = startScripts.optsEnvironmentVar
        outputDir = startScripts.outputDir
        unixStartScriptGenerator = startScripts.unixStartScriptGenerator
        windowsStartScriptGenerator = startScripts.windowsStartScriptGenerator
    }
    startScripts.dependsOn(t)
}

dependencies {
    implementation project(':builder')
}
