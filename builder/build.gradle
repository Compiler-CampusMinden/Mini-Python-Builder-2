plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'de.hsbi.mini-python-builder'
version = '0.0.1'

repositories {
    mavenCentral()
}

processResources {
    // copy the folder as is into the jar-archive,
    // thus making it available as a resource with path 'c-runtime'
    from('../c-runtime') {
        into('c-runtime')
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.1'
    // allow reuse of the mini-python program code in
    // the ManualTest class (from the examples)
    // for automatic tests of the builder itself
    testImplementation project(':builder-examples')
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        builder(MavenPublication) {
            from components.java
            artifactId = 'mini-python-builder'
        }
    }

    repositories {
        maven {
            name = 'local'
            url = layout.buildDirectory.dir("mvn-repo")
        }
    }
}
